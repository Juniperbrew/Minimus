import com.badlogic.gdx.tools.texturepacker.TexturePacker;

apply plugin:'application'

sourceCompatibility = 1.7
version = '0.01'
mainClassName = "com.juniperbrew.minimus.client.ClientLauncher"
applicationName = "Minimus"


ext {
    appName = 'Minimus'
    gdxVersion = '1.6.4'
    texturePacker = ["resources/images/raw", "resources/images", "sprites"]
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'com.miglayout:miglayout-swing:4.2'
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.esotericsoftware:kryonet:2.22.0-RC1"
    compile 'org.apache.commons:commons-collections4:4.0'
}

buildscript {

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.badlogicgames.gdx:gdx-tools:1.6.4"
    }
}

installDist {
    into('bin') {
        from '.'
        include 'resources/**'
    }
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

task texturePacker << {
    if (project.ext.has('texturePacker')) {
        logger.info "Calling TexturePacker: "+texturePacker
        TexturePacker.process(texturePacker[0], texturePacker[1], texturePacker[2])
    }
}

task installServer(type: CreateStartScripts) {
    mainClassName = "com.juniperbrew.minimus.server.ServerLauncher"
    applicationName = "MinimusServer"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(installServer)
    fileMode = 0755
}